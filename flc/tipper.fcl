// Block definition (there may be more than one block per file)
FUNCTION_BLOCK tipper

// Define input variables
VAR_INPUT
    ica : REAL;
    water_temperature : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    probability : REAL;
END_VAR

// Input
FUZZIFY ica
    TERM awful := (0, 1) (27.5, 0) ; 
    TERM bad := (22.5, 0) (30,1) (45,1) (52.5,0);
    TERM regular := (47.5, 0) (55,1) (67.2,1) (72.5,0);
    TERM good := (67.2, 0) (75,1) (87.5,1) (92.5,0);
    TERM excellent := (87.5, 0) (100, 1);
END_FUZZIFY

// Input
FUZZIFY water_temperature
    TERM low := (0, 1) (25, 0) ;
    TERM ideal := (22.5, 0) (25, 1) (27.5, 1) (30, 0);
    TERM high := (27.5, 0) (30, 1) (37.5, 1) (40, 0);
    TERM very_high := (37.5,0) (50,1);
END_FUZZIFY

// Output
DEFUZZIFY probability
    TERM low := (0,0) (5,1) (10,0);
    TERM average := (10,0) (15,1) (20,0);
    TERM high := (20,0) (25,1) (30,0);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

//			ica         awful     bad       regular     good       excellent
//	water_temperature        
//	low                 low       low         low       average     average    
//  ideal               low       low         high      high        high       
//	high			    low       low         low       average     average    
//	very_high			low       low         low       low         low    
//

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF ica IS awful AND water_temperature IS low 
                THEN probability IS low;

    RULE 2 : IF ica IS bad AND water_temperature IS low
                THEN probability IS low; 

    RULE 3 : IF ica IS regular AND water_temperature IS low 
                THEN probability IS low;
    
    RULE 4 : IF ica IS good AND water_temperature IS low 
                THEN probability IS average;
    
    RULE 5 : IF ica IS excellent AND water_temperature IS low 
                THEN probability IS average;
    
    RULE 6 : IF ica IS awful AND water_temperature IS ideal 
                THEN probability IS low;
                
    RULE 7 : IF ica IS bad AND water_temperature IS ideal 
                THEN probability IS low;
                
    RULE 8 : IF ica IS regular AND water_temperature IS ideal 
                THEN probability IS high;
                
    RULE 9 : IF ica IS good AND water_temperature IS ideal 
                THEN probability IS high;
                
    RULE 10 : IF ica IS excellent AND water_temperature IS ideal 
                THEN probability IS high;
                
    RULE 11 : IF ica IS awful AND water_temperature IS high 
                THEN probability IS low;
                
    RULE 12 : IF ica IS bad AND water_temperature IS high 
                THEN probability IS low;
                
    RULE 13 : IF ica IS regular AND water_temperature IS high 
                THEN probability IS low;
                
    RULE 14 : IF ica IS good AND water_temperature IS high 
                THEN probability IS average;

    RULE 15 : IF ica IS excellent AND water_temperature IS high 
                THEN probability IS average;

    RULE 16 : IF ica IS awful AND water_temperature IS very_high 
                THEN probability IS low;
                
    RULE 17 : IF ica IS bad AND water_temperature IS very_high 
                THEN probability IS low;
                
    RULE 18 : IF ica IS regular AND water_temperature IS very_high 
                THEN probability IS low;
                
    RULE 19 : IF ica IS good AND water_temperature IS very_high 
                THEN probability IS low;

    RULE 20 : IF ica IS excellent AND water_temperature IS very_high 
                THEN probability IS low;
END_RULEBLOCK

END_FUNCTION_BLOCK