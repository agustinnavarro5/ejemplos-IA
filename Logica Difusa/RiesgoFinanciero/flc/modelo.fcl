// Definición de Bloque
FUNCTION_BLOCK Modelo_Riesgo_Financiero

// Variables de entrada
VAR_INPUT
    edad : REAL;
    porcentaje_manejo : REAL;
END_VAR

// Variable de salida
VAR_OUTPUT
    riesgo : REAL;
END_VAR

// Entrada
FUZZIFY edad
    TERM joven := (0, 1) (25, 1) (30, 0) ; 
    TERM adulto := (20, 0) (35, 1) (50, 0);
    TERM mayor := (40, 0) (60, 1) (70, 1);
END_FUZZIFY

// Entrada
FUZZIFY porcentaje_manejo
    TERM bajo := (0, 1) (10, 1) (20,0) ;
    TERM medio := (10, 0) (40, 1) (60, 0);
    TERM alto := (50,0) (70,1) (100, 1);
END_FUZZIFY

// Salida
DEFUZZIFY riesgo
    TERM bajo := (0, 1) (10, 1) (20, 0);
    TERM medio := (10, 0) (30, 1) (45, 0);
    TERM alto := (40, 0) (55, 1) (100, 1);
    
    // Centro de Gravedad
    METHOD : COG;
    // Por defecto el valor es 0 (Si ninguna regla es activada)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Usa 'min' para condición 'and'
    AND : MIN;
    // Usa 'min' metodo de activación
    ACT : MIN;
    // Usa 'max' metodo acumulación
    ACCU : MAX;

    RULE 1 : IF edad IS joven AND porcentaje_manejo IS bajo 
                THEN riesgo IS medio;

    RULE 2 : IF edad IS joven AND porcentaje_manejo IS medio
                THEN riesgo IS alto; 

    RULE 3 : IF edad IS joven AND porcentaje_manejo IS alto 
                THEN riesgo IS alto;
    
    RULE 4 : IF edad IS adulto AND porcentaje_manejo IS bajo 
                THEN riesgo IS bajo;
                
    RULE 5 : IF edad IS adulto AND porcentaje_manejo IS medio 
                THEN riesgo IS medio;
                
    RULE 6 : IF edad IS adulto AND porcentaje_manejo IS alto 
                THEN riesgo IS alto;
                
    RULE 7 : IF edad IS mayor AND porcentaje_manejo IS bajo
                THEN riesgo IS medio;
                
    RULE 8 : IF edad IS mayor AND porcentaje_manejo IS medio 
                THEN riesgo IS alto;
                
    RULE 9 : IF edad IS mayor AND porcentaje_manejo IS alto 
                THEN riesgo IS alto;
END_RULEBLOCK

END_FUNCTION_BLOCK